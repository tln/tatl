(* Grako grammer for TATL

The special attributes use *Attr rules. The substitutions use 'top'.
'attrs' is used for testing.

Rules are indented underneath the rule where they are first referenced; 
if they refer to a rule further up, a comment on left margin indicates the
rule.

Generates tatl/ExprParser.py; also see tatl/ExprSemantics.py for the corresponding
AST manipulations.

*)


(* Rules used to parse special attribute values *)
defAttr = defExpr $ ;
forAttr = forExpr $ ;
ifAttr = ifExpr $ ;
paramAttr = paramExpr $ ;
setAttr = setExpr $ ;
useAttr = useExpr $ ;

(* For testing *)
attrs = 'def="' @defExpr '"' 
	| 'for="' @forExpr '"'
    | 'if="' @ifExpr '"' 
	| 'param="' @paramExpr '"'
	| 'set="' @setExpr '"' 
	| 'use="' @useExpr '"' 
	| top ;

defExpr = name:name [args:arglist] ['=' result:expr] {filter+:callfilter} ;
	arglist = '(' [@+:arg {',' @+:arg} [',']] @+:')' ;
	arg = @name | @'*' ;
	name = NAME ;
	callfilter = filter ;
setExpr = lvar ;
forExpr = {set+:set ';'} [n1:lvar [',' n2:lvar] 'in'] expr:expr ;
ifExpr = {set+:set ';'} test:test ;
paramExpr = @+:lvar {',' @+:lvar} ;
useExpr = {set+:set ';'} path:path [arglist:callargs];
	callargs = '(' [@+:expr {',' @+:expr} [',']] ')';

(* Substitutions use this rule *)
top = '{' {set+:set ';'} (set+:set | ({exprs+:expr ';'} [emit:topemitexpr])) '}';

	set = lset | setif ;
	lset = lvar:lvar '=' expr:expr ;
		lvar = name ;
	setif = var:name ('=?'|'?=') expr:expr ;

	topemitexpr = placeholder | filtexp ;
		filtexp = expr:expr {filter+:exprfilter} ;
		exprfilter = filter ;
		filter = '|' (  @call | @path ) ;
		placeholder = @('*' | '++') ':' @name ;


	expr = ternary | range | simpleexpr;

		ternary = test:test ('?' true:expr [':' false:expr] | '?:' false:expr);
(*expr*)	test = regex | comp ;   
				regex = expr:simpleexpr op:('~!'|'!~'|'~') re:relit ;
					relit = REGEX ;
				comp = @+:simpleexpr ( {@+:compop @+:simpleexpr}+ 
				                     | [@+:eqop @+:simpleexpr] 
									 ) ;
					eqop = '==' | '!=' | 'eq' | 'ne';
					compop = 'le'|'ge'|'gt'|'lt'|'<='|'=<'|'<'|'>='|'=>'|'>';

		range = simpleexpr (  '...' | '..' ) simpleexpr ;

		simpleexpr = call | path | value | list | map ;

(*expr*)	call = fn:path '(' [arg+:expr {',' arg+:expr} [',']] ')' ;

			path = path:(externalPath | dotPath | dottedPath) lookup:[lookup] ;
				dottedPath =  @+:name {'.' @+:pname}* ;
				dotPath = @+:'.' [@+:pname {'.' @+:pname}*] ;
				externalPath = module:pname '::' path+:pname {'.' path+:pname} ;
					pname = NAME ; 
(*simpleexpr*)	lookup = '[' @simpleexpr ']' ;

			value = number | string ;
				number = NUMBER ;
				string = STRING ;


(*expr*)	list = '[' [@+:(expr|starexp) {',' @+:(expr|starexp)} [',']]  ']' ;
(*expr*)		starexp = '*' @expr ;

			map = '{' [@+:(member|starexp) {',' @+:(member|starexp)}] '}' ;
				member = ( nkey:NAME | skey:STRING) ':' val:expr ;

NAME = ?/[a-zA-Z][a-zA-Z0-9_]*/? ;
REGEX = ?/[/].*?[/]/?;
INT = ?/[0-9]+/? ;
DOT = '.' ;
DOTS =   DOT | '.' {'.'}+ ;
NUMBER = ?/-?[0-9]+([.][0-9]+([eE][+-]?[0-9]+)?)?/? ;
WS = (  ' ' | '\t') ;
NL =   '\n' | '\r' ;
STRING = ?/(['"]).*?\1/?;
PYEXPR = '(' O [PYEXPR] O ')';
O = ?/[^()]*/?;

