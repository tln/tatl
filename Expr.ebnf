attrs = 'def="' @defExpr '"' 
	| 'for="' @forExpr '"'
    | 'if="' @ifExpr '"' 
	| 'param="' @paramExpr '"'
	| 'set="' @setExpr '"' 
	| 'use="'  useExpr '"' 
	| top ;
defExpr = name:NAME args:arglist {filter+:filter} ['=' result:expr];
setExpr = set+:set {';' set+:set} ;
arglist = '(' [@+:arg {',' @+:arg} [',']] ')' ;
arg = @NAME | @'*' ;
forExpr = {set+:set ';'} [n1:lvar [',' n2:lvar] 'in'] expr:expr ;
ifExpr = {set+:set ';'} @test ;
paramExpr = @+:lvar {',' @+:lvar} ;
useExpr = {set+:set ';'} path:path [arglist:callargs];
callargs = '(' [expr {',' expr} [',']] ')';

top = '{' {set+:set ';'} (set+:set | ({exprs+:expr ';'} [emit:topemitexpr])) '}' ;

	set = lvar:lvar op:('='|'=?'|'?=') expr:expr ;
		lvar = NAME ;

	topemitexpr = star:starexp | filtexp:filtexp ;
		filtexp = expr:expr {filter+:filter} ;
		filter = '|' (  @call | @path | @string) ;
		starexp = @('*' | '++') ':' @NAME ;


	expr = ternary | range | simpleexpr;

		ternary = test:test ('?' true:expr [':' false:expr] | '?:' false:expr);
(*expr*)	test = regex | comp;
				regex = expr:simpleexpr op:('~'|'!~'|'~!') re:REGEX ;
				comp = @+:simpleexpr ( {@+:compop @+:simpleexpr}+ 
				                     | [@+:eqop @+:simpleexpr] 
									 ) ;
					eqop = '==' | '!=' ;
					compop = '<=' | '=<' | '<' | '>=' | '=>' | '>';

		range = simpleexpr (  '...' | '..' ) simpleexpr ;

		simpleexpr = call | path | value | list | map ;

(*expr*)	call = fn:path '(' [arg+:expr {',' arg+:expr} [',']] ')' ;

			path = path:(externalPath | dotPath | dottedPath) lookup:[lookup] ;
				dottedPath =  @+:NAME {'.' @+:NAME}* ;
				dotPath = @+:'.' [@+:NAME {'.' @+:NAME}*] ;
				externalPath = module:NAME '::' path+:NAME {'.' path+:NAME} ;
(*simpleexpr*)	lookup = '[' @simpleexpr ']' ;

			value = number | string ;
				number = NUMBER ;
				string = STRING ;


(*expr*)	list = '[' [@+:(expr|starexp) {',' @+:(expr|starexp)} [',']]  ']' ;
(*expr*)		starexp = '*' expr ;

			map = '{' [@+:(member|starexp) {',' @+:(member|starexp)}] '}' ;
				member = ( nkey:NAME | skey:STRING) ':' val:expr ;

REGEX = ?/[/].*?[/]/?;
NAME = ?/[a-zA-Z_][a-zA-Z0-9_]*/? ;
INT = ?/[0-9]+/? ;
DOT = '.' ;
DOTS =   DOT | '.' {'.'}+ ;
NUMBER = ?/-?[0-9]+([.][0-9]+([eE][+-]?[0-9]+)?)?/? ;
WS = (  ' ' | '\t') ;
NL =   '\n' | '\r' ;
STRING = ?/(['"]).*?\1/?;
PYEXPR = '(' O [PYEXPR] O ')';
O = ?/[^()]*/?;

